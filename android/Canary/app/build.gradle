apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
keystorePropertiesFile.exists()

def keystoreProperties = new Properties()
if(keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else {
    keystoreProperties.setProperty('keyAlias', '')
    keystoreProperties.setProperty('keyPassword', '')
    keystoreProperties.setProperty('storeFile', ' ')
    keystoreProperties.setProperty('storePassword', '')
}

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion = 27
    buildToolsVersion = '27.0.1'

    defaultConfig {
        applicationId = 'me.seanmaltby.canary'
        minSdkVersion 21
        targetSdkVersion 27
        versionCode = 1
        versionName = '1.0'
        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi', 'armeabi-v7a', 'arm64-v8a'
        }
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_PLATFORM=android-21',
                        '-DANDROID_TOOLCHAIN=clang', '-DANDROID_STL=c++_shared'
                cppFlags "-std=c++11 -frtti -fexceptions"
            }
        }
    }
    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled false
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            // let gradle pack the shared library into apk
            jniLibs.srcDirs = ['../distribution']
        }
    }
    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    google()
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.spotify.sdk:spotify-auth-beta23-noconnect-2.20b@aar'
    implementation 'com.spotify.sdk:spotify-player-beta23-noconnect-2.20b@aar'
    implementation 'org.webrtc:google-webrtc:1.0.+'
    implementation project(':models')
    implementation 'edu.cmu.pocketsphinx:pocketsphinx-android-5prealpha-release@aar'
}
